<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml"
                title="DecisionWindow"
                x="160" y="80" status="active"
                close="close()"
                showCloseButton="true" borderAlpha="0.75">
    <mx:Form>
        <mx:FormHeading label="Decyzja o urlopie" />
        <mx:FormItem label="Pracownik:">
            <mx:Text text="{fullname}"/>
        </mx:FormItem>
        <mx:FormItem label="Od:">
            <mx:Text text="{startingDate.toLocaleDateString()}"/>
        </mx:FormItem>
        <mx:FormItem label="Do:">
            <mx:Text text="{endingDate.toLocaleDateString()}"/>
        </mx:FormItem>
        <mx:FormItem label="Powód:">
            <mx:TextInput text="{cause}"/>
        </mx:FormItem>
        <mx:HBox>
            <mx:Button label="Zatwierdź" cornerRadius="12" click="sendDecision(DataLabel.STATUS_ACCEPTED)"/>
            <mx:Button label="Odrzuć" cornerRadius="12" click="sendDecision(DataLabel.STATUS_REJECTED)"/>
        </mx:HBox>
    </mx:Form>
    <mx:Script><![CDATA[
        import flex.admin.components.DataLabel;
        import flex.admin.model.HolidayExtDTO;
        import flex.admin.services.AdminDataService;
        import flex.admin.services.impl.AdminDataServiceImpl;
        import flex.data.Holiday;
        import flex.service.BaseService;

        import mx.controls.Alert;

        import mx.managers.PopUpManager;
        import mx.rpc.events.ResultEvent;

        public var decisionHandler: Function;

        private var dtoId:Number;
        [Bindable]
        private var fullname:String = "";
        private var login:String = "";
        [Bindable]
        private var cause:String = "";
        [Bindable]
        private var startingDate:Date = new Date();
        [Bindable]
        private var endingDate:Date = new Date();
        private var adminDataService:AdminDataService = new AdminDataServiceImpl();

        public function fetchData(gLogin:String, gDay:Date):void {
            this.login = gLogin;
            adminDataService.fetchHoliday(login, gDay, update);
        }


        public function sendDecision(decisionStatus:int):void {
            adminDataService.sendDecision(dtoId, decisionStatus, "", handleSendRequest);
            close();
        }

        private function handleSendRequest(event:ResultEvent):void {
            if(decisionHandler!=null){
                decisionHandler();
            } else {
                Alert.show("DH null!");
            }
            Alert.show("Decision sent succesfully");
            AdminDataServiceImpl(adminDataService).disconnect();
        }

        private function update(event:ResultEvent):void {
            var dto:HolidayExtDTO = HolidayExtDTO(event.result);
            this.fullname = dto.firstName + " " + dto.secondName;
            this.startingDate = dto.dateFrom;
            this.endingDate = dto.dateTo;
            this.cause = dto.cause;
            this.dtoId = dto.id;
        }

        private function close():void {
            PopUpManager.removePopUp(this);
        }
        ]]></mx:Script>
</mx:TitleWindow>